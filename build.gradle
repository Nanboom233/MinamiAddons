plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.0.0'
    id 'fabric-loom' version '1.10-SNAPSHOT'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = JavaVersion.VERSION_21

repositories {
    mavenCentral()
    maven {
        url = 'https://maven.terraformersmc.com/releases/'
    }
    maven {
        url = "https://repo.essential.gg/repository/maven-public"
    }
    maven {
        url = 'https://masa.dy.fi/maven'
    }
    maven {
        url = "https://jitpack.io"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings_version}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    include(modApi(fabricApi.module("fabric-api-base", project.fabric_api_version)))
    include(modApi(fabricApi.module("fabric-networking-api-v1", project.fabric_api_version)))
    include(modApi(fabricApi.module("fabric-resource-loader-v0", project.fabric_api_version)))

    modCompileOnly "com.terraformersmc:modmenu:${project.mod_menu_version}"
    modCompileOnly "com.github.sakura-ryoko:litematica:${project.minecraft_version}-${project.litematica_version}"
    modCompileOnly "com.github.sakura-ryoko:malilib:${project.minecraft_version}-${project.malilib_version}"

    modImplementation(include("gg.essential:universalcraft-1.21.6-fabric:415"))
    modImplementation(include("gg.essential:elementa:710"))
    modImplementation(include("gg.essential:vigilance:306"))

    implementation(include("org.reflections:reflections:0.10.2"))
}

group = project.group + "." + project.mod_id
archivesBaseName = project.mod_name + "-fabric-" + project.yarn_mappings_version
version = project.mod_version

if (version.endsWith('-dev')) {
    version += "." + new Date().format('yyyyMMdd.HHmmss')
}

processResources {
    exclude '**/*.xcf'
    exclude '**/xcf'

    inputs.property "mod_version", project.mod_version

    filesMatching("fabric.mod.json") {
        expand "mod_version": project.mod_version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 21
}

tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = true
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archivesBaseName
            from components.java
        }
    }
    repositories {}
}